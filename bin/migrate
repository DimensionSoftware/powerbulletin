#!./node_modules/.bin/lsc

require! {
  \fs
  \async
  program: \commander
  pg: '../app/postgres'
  models: '../app/pb-models'
}
global <<< require \prelude-ls

err <- pg.init
if err then console.warn "Unable to init DB"; process.exit 1 # guard
global.db = pg.procs
err <- models.init
if err then console.warn "Unable to init models"; process.exit 2
# mixing additional keys into 'db' namespace
do -> db <<< { [k,v] for k,v of models when k not in <[orm client driver]> }

defaults =
  id: 1

program
  .usage '[OPTION]...'
  .description 'Default is to migrate up to the latest'
  .option  '-m, --migration', "print current migration"
  .parse process.argv

const valid      = /^(\d+)-/
const migrations = if process.env.NODE_ENV is \production then \/pb/migrations else \/vagrant/migrations
const table-name = \_version
const log        = console.log
const create-sql = """
  CREATE TABLE #table-name (n int DEFAULT 0);
  INSERT INTO #table-name VALUES (0) RETURNING *;
"""

init = (cb) ->
  # return current version
  err, r <- pg.query "SELECT n FROM #table-name LIMIT 1", []
  if err
    # no version, so--create table w/ defaults
    log 'Initializing migrations ...'
    err, r <- pg.query create-sql, []
    if err
      log "Unable to create #table-name"
      process.exit 1
    else
      cb null, r
  else
    cb null, r

migrations-available = (path, cur-version, cb) ->
  err, files <- fs.readdir migrations
  if err then return cb err

  numeric = (a, b) -> # sort numeric
    an = parse-int (a.match valid).1
    bn = parse-int (b.match valid).1
    an - bn

  sorted = files
    |> filter (-> # scrub invalid files
      m = it.match valid
      parse-int(m?1) and m?1 > cur-version) # already ran these
    |> sort-with numeric
    |> map (.replace /\.\w+$/, '')
  cb null, sorted

update-version = (client, file, cb) ->
  v = parse-int file.match(valid)?1
  err, r <- client.query "UPDATE #table-name SET n=$1 RETURNING *", [v]
  cb err, r

migrate-up = (file, cb) ->
  rollback = (client, done=(->)) ->
    log "Rolling back #file"
    err <- client.query \ROLLBACK, []
    done err
    process.exit 1

  try
    m = require "#migrations/#file"
  catch
    cb "Unable to require #file"

  if m.up
    # converge migration & version update into single transaction
    err, client, done <- pg.connect
    # run migration in a transaction
    err, r <- client.query \BEGIN, []
    if err then rollback client; return
    err <- m.up client
    if err then rollback!; return
    # update version
    err, v <- update-version client, file
    if err then cb err; return
    log "`- migrated #file OK"
    err <- client.query \COMMIT, []
    if err then rollback!; return
    cb null, v; done! if done
  else
    cb "Invalid migration #file"

####  main  ;,.. ___  _
err, [version] <- init
if program.migration
  log "@ migration #{version.n}"
  process.exit 0

# TODO (re)run a specific migration

else # run all outstanding migrations
  err, files <- migrations-available migrations, version.n
  unless files then log 'No migrations found!'; process.exit 1
  err <- async.each-series files, migrate-up
  if err then throw err
  err, [new-version] <- init
  log "@ migration #{new-version.n}"
  process.exit 0


# vim:ft=ls
