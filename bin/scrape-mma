#!./node_modules/.bin/lsc

require! {
  \fs
  \jsdom
  \async
  \util
  program: \commander
  pg: '../app/postgres'
  m: '../app/pb-models'
  varnish: '../app/varnish'
  sioa: 'socket.io-announce'
}
global <<< require \prelude-ls
announce = sioa.create-client!
err <- pg.init
if err then console.warn(err)
err <- m.init
if err then console.warn(err)
db = pg.procs
db <<< { [k,v] for k,v of m when k not in <[orm client driver]> }

url = # {{{1
  forum: (forum-id) ->
    "http://www.mixedmartialarts.com/mma.cfm?go=forum.splash&forum=#{forum-id}"
  thread: (forum-id, thread-id, page=1) ->
    "http://www.mixedmartialarts.com/mma.cfm?go=forum.posts&thread=#{thread-id}&forum=#{forum-id}&page=#{page}&pc=143"

threads-from-forum = (forum-id, cb) -> # {{{1
  threads = []
  console.log "fetching forum #{forum-id}"
  jsdom.env(
    url.forum(forum-id),
    ["http://code.jquery.com/jquery.js"],
    (err, window) ->
      if err then return cb(err)
      $ = window.$
      $('.ThreadList td.First').each (i, td) ->
        $a = $(td).find('a:first')
        count = parseInt $(td).next().next().text()
        pages = Math.ceil(count / 25)
        if $a.length
          id    = $a.attr('href').match(/thread=(\d+)/)?[1]
          title = $a.text()
          threads.push {
            thread-id : id
            forum-id  : forum-id
            title     : title
            posts     : [ ]
            pages     : pages
            count     : count
          }
      cb null, threads
  )

with-posts = (max, t, cb) --> # {{{1
  forum-id = t.forum-id
  thread-id = t.thread-id
  actual-pages = Math.min(max, t.pages)
  console.log "fetching #{actual-pages} pages of posts for thread - #{t.title}"
  fetch-posts = (page, cb) ->
    util.print "#page "
    jsdom.env(
      url.thread(forum-id, thread-id, page)
      ["http://code.jquery.com/jquery.js"],
      (err, window) ->
        if err then return cb(err)
        $ = window.$
        _posts = []
        $('.PostList .PostContent').each (i, p) ->
          $p = $ p
          $p.find("img[src$='apple.png'],img[src$='droid.png']").remove!
          first-image = $p.find \img:first .attr \src
          first-image = null if first-image?length > 2000
          images = []
          $p.find \img .each (i, img) ->
            images.push $(img).attr('src')
          images = unique images
          html = $p.html!
          post =
            parent-id : thread-id
            forum-id  : forum-id
            user-id   : 3
            title     : ""
            media-url : first-image or ''
            images    : images
            body      : html
            html      : html
          _posts.push post
        cb null, _posts
    )
  async.map-series [1 to actual-pages], fetch-posts, (err, posts) ->
    util.print "\n"
    t.posts = flatten posts
    t.body = t.posts?0?body || "\n"
    t.html = t.posts?0?html || "\n"
    t.media_url = t.posts?0?media-url
    t.posts.shift!
    cb null, t

create-thread-for-site = (site-id, t, cb) --> # {{{1
  console.log "\ninserting top post f:#{t.forum-id} title:'#{t.title}'"
  top-post =
    user_id    : 1
    forum_id   : t.forum-id
    title      : t.title
    body       : t.body
    html       : t.html
    media_url  : t.media_url
    build_docs : false

  emit = (type, id, len) ->
    err, new-post <- pg.procs.post id
    if not err
      new-post.post_count = len
      announce.emit type, new-post

  pg.procs.add-post top-post, (err, insertion) ->
    if err then return cb(err)
    emit \thread-create, insertion.id, t.posts?length

    create-post = (p, cb) ->
      q = {}
      for k,v of p
        key = k.replace(/([A-Z])/g, ($1) -> return "_"+$1.toLowerCase())
        q[key] = v
      q.thread_id = q.parent_id = insertion.id
      q.build_docs = false

      pg.procs.add-post q, (err, r) ->
        if r?success and not err
          util.print "."
          emit \post-create, r.id
        else
          console.error \add-post, err
        if r?success and not err and q.images
          add-images = (url, cb) ->
            i =
              post_id   : r.id
              thread_id : q.thread_id
              is_local  : false
              url       : url
            util.print \i
            db.images.upsert i, cb
          async.each-series q.images, add-images, (err) ->
            cb(err, r)
        else
          cb(err, r)

    t.posts.shift!
    async.map-series t.posts, create-post, cb

delay-by = (ms, fn) --> # {{{1
  (param, cb) ->
    (setTimeout (-> fn(param, cb)), ms)

# }}}
#.
#### main   ###############>======-- -   -
##
#
#
#

defaults =
  site  : 2
  forum : 1
  delay : 1500
  max   : 3

program
  .option  '-s, --site <id>',            "site id  (default: #{defaults.site})",                                         parseInt, defaults.site
  .option  '-f, --forum <id>',           "forum id (default: #{defaults.forum})",                                        parseInt, defaults.forum
  .option  '-d, --delay <milliseconds>', "amount of time to wait in between HTTP requests (default: #{defaults.delay})", parseInt, defaults.delay
  .option  '-m, --max <pages>',          "maximum pages per thread to scrape (default: #{defaults.max})",                parseInt, defaults.max
  .parse process.argv

delayed = delay-by program.delay # gratuitous use of partial application ;)

err, threads <- threads-from-forum program.forum
if err then console.warn err
console.log "found #{threads.length} threads for forum #{program.forum}"

with-posts-max = with-posts program.max
err, threads-with-posts <- async.map-series threads, (delayed with-posts-max)
if err then console.warn err

err, r <- async.map-series threads-with-posts, (create-thread-for-site program.site)
if err then console.warn(err)
console.log "\n"

err <- varnish.init
if err then console.warn(err)

err <- varnish.ban-all
if err then console.warn(err)

process.exit 0

# vim:ft=ls fdm=marker
